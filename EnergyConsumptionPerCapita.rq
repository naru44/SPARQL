#+ endpoint: http://semantic.eea.europa.eu/sparql
#+ summary: Retrieves Energy Consumption per Household per Capita in Europe
#+ tags: 
#+  - Environment
#+  - Energy


PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#> 
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmx-attribute: <http://purl.org/linked-data/sdmx/2009/attribute#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX property: <http://rdfdata.eionet.europa.eu/eurostat/property#> 
PREFIX unit: <http://dd.eionet.europa.eu/vocabulary/eurostat/unit/>
PREFIX product: <http://dd.eionet.europa.eu/vocabulary/eurostat/product/>
PREFIX indic_nrg: <http://dd.eionet.europa.eu/vocabulary/eurostat/indic_nrg/>
PREFIX geo: <http://dd.eionet.europa.eu/vocabulary/eurostat/geo/>
PREFIX indic_de: <http://dd.eionet.europa.eu/vocabulary/eurostat/indic_de/>
PREFIX unit: <http://dd.eionet.europa.eu/vocabulary/eurostat/unit/>
PREFIX bif: <http://www.openlinksw.com/schemas/bif#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT 
year(?date) as ?date
bif:either(?geo = geo:DE, 'Germany', ?ugeo) as ?ugeo
round(((sum(?nrg_100a)*1000)/sum(?demo_gind))*100)/100.0 as ?index
sum(?demo_gind) as ?demo_gind
sum(?nrg_100a) as ?nrg_100a
WHERE {
{

  GRAPH <http://rdfdata.eionet.europa.eu/eurostat/data/demo_gind.rdf.gz> 
    {
    _:demo_gind sdmx-dimension:timePeriod ?date .
    _:demo_gind sdmx-dimension:refArea ?geo .
    _:demo_gind property:indic_de indic_de:AVG .
    _:demo_gind sdmx-measure:obsValue ?demo_gind .
    }
} UNION {
  GRAPH <http://rdfdata.eionet.europa.eu/eurostat/data/nrg_100a.rdf.gz> 
  {
    _:nrg_100a sdmx-dimension:refArea ?geo .
    _:nrg_100a sdmx-attribute:unitMeasure unit:KTOE .
    _:nrg_100a sdmx-dimension:timePeriod ?date .
    _:nrg_100a property:product product:0000 .
    _:nrg_100a property:indic_nrg indic_nrg:B_102010 .      
    _:nrg_100a sdmx-measure:obsValue ?nrg_100a .

  }
 } 
?geo rdfs:label ?ugeo .
FILTER (year(?date) in (2005, 2016))
} 
GROUP BY year(?date) ?geo ?ugeo
HAVING (sum(?nrg_100a)/sum(?demo_gind) > 0)
ORDER BY ?date ?ugeo
